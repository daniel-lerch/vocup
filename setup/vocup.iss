; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
#define LongAppVersion GetFileVersion("..\src\Vocup\bin\Release\net6.0-windows10.0.19041.0\win-x86\publish\Vocup.exe")
#define AppVersion Copy(LongAppVersion, 1, RPos(".", LongAppVersion) - 1)

AppName=Vocup
AppVerName=Vocup {#AppVersion}
AppVersion={#AppVersion}
AppPublisher=Daniel Lerch
DefaultDirName={commonpf}\Vocup
DisableProgramGroupPage=yes
OutputDir=bin
SetupIconFile=setup_icon.ico

Compression=lzma2
SolidCompression=true
ShowLanguageDialog=no
AppMutex=Vocup
MinVersion=6.1sp1
DirExistsWarning=no
ChangesAssociations=yes

WizardImageFile=left.bmp
WizardSmallImageFile=top.bmp

LicenseFile=license.txt

VersionInfoVersion={#AppVersion}
VersionInfoCompany=Daniel Lerch
VersionInfoCopyright=© 2011 Florian Amstutz, © 2018-2022 Daniel Lerch

UninstallDisplayIcon={app}\Vocup.exe
UninstallDisplayName=Vocup (classic installation)

[Languages]
Name: german; MessagesFile: compiler:Languages\German.isl;

[Tasks]
Name: desktopicon; Description: {cm:CreateDesktopIcon}; GroupDescription: {cm:AdditionalIcons};

[Files]
Source: ..\src\Vocup\bin\Release\net6.0-windows10.0.19041.0\win-x86\publish\Resources\easter_egg.vhf;    DestDir: {app}\Resources; Flags: ignoreversion replacesameversion;
Source: ..\src\Vocup\bin\Release\net6.0-windows10.0.19041.0\win-x86\publish\Resources\help.chm;          DestDir: {app}\Resources; Flags: ignoreversion replacesameversion;
Source: ..\src\Vocup\bin\Release\net6.0-windows10.0.19041.0\win-x86\publish\D3DCompiler_47_cor3.dll;     DestDir: {app}; Flags: ignoreversion replacesameversion;
Source: ..\src\Vocup\bin\Release\net6.0-windows10.0.19041.0\win-x86\publish\PenImc_cor3.dll;             DestDir: {app}; Flags: ignoreversion replacesameversion;
Source: ..\src\Vocup\bin\Release\net6.0-windows10.0.19041.0\win-x86\publish\PresentationNative_cor3.dll; DestDir: {app}; Flags: ignoreversion replacesameversion;
Source: ..\src\Vocup\bin\Release\net6.0-windows10.0.19041.0\win-x86\publish\vcruntime140_cor3.dll;       DestDir: {app}; Flags: ignoreversion replacesameversion;
Source: ..\src\Vocup\bin\Release\net6.0-windows10.0.19041.0\win-x86\publish\Vocup.dll.config;            DestDir: {app}; Flags: ignoreversion replacesameversion;
Source: ..\src\Vocup\bin\Release\net6.0-windows10.0.19041.0\win-x86\publish\Vocup.exe;                   DestDir: {app}; Flags: ignoreversion replacesameversion;
Source: ..\src\Vocup\bin\Release\net6.0-windows10.0.19041.0\win-x86\publish\Vocup.pdb;                   DestDir: {app}; Flags: ignoreversion replacesameversion;
Source: ..\src\Vocup\bin\Release\net6.0-windows10.0.19041.0\win-x86\publish\wpfgfx_cor3.dll;             DestDir: {app}; Flags: ignoreversion replacesameversion;

; Icons for file associations
Source: icon_vhf.ico;                 DestDir: {app}; Flags: ignoreversion replacesameversion;
Source: icon_vdp.ico;                 DestDir: {app}; Flags: ignoreversion replacesameversion;

[InstallDelete]
Type: files; Name: {app}\de\Vocup.resources.dll;
Type: files; Name: {app}\CsvHelper.dll;
Type: files; Name: {app}\Hilfe.chm;
Type: files; Name: {app}\ICSharpCode.SharpZipLib.dll;
Type: files; Name: {app}\K_Updater.dll;
Type: files; Name: {app}\Microsoft.Bcl.AsyncInterfaces.dll;
Type: files; Name: {app}\Microsoft.Bcl.HashCode.dll;
Type: files; Name: {app}\Octokit.dll;
Type: files; Name: {app}\PInvoke.Kernel32.dll;
Type: files; Name: {app}\PInvoke.User32.dll;
Type: files; Name: {app}\PInvoke.Windows.Core.dll;
Type: files; Name: {app}\PInvoke.Windows.ShellScalingApi.dll;
Type: files; Name: {app}\sound_correct.wav;
Type: files; Name: {app}\sound_wrong.wav;
Type: files; Name: {app}\System.Buffers.dll;
Type: files; Name: {app}\System.Memory.dll;
Type: files; Name: {app}\System.Numerics.Vectors.dll;
Type: files; Name: {app}\System.Runtime.CompilerServices.Unsafe.dll;
Type: files; Name: {app}\System.Threading.Tasks.Extensions.dll;
Type: files; Name: {app}\System.ValueTuple.dll;
Type: files; Name: {app}\Validation.dll;
Type: files; Name: {app}\Vocup.exe.config;

[Icons]
Name: {commonprograms}\Vocup;         Filename: {app}\Vocup.exe;
Name: {commondesktop}\Vocup;          Filename: {app}\Vocup.exe; Tasks: desktopicon;

[Registry]

;Mit dieser Anweisung wird ein Registryschlüssel namens ".vhf" erzeugt
Root: HKCR; Subkey: ".vhf";                        ValueType: string; ValueName: ""; ValueData: "vhf.File";                   Flags: uninsdeletekey deletevalue;
;TypenSchlüssel erstellen
Root: HKCR; Subkey: "vhf.File";                    ValueType: string; ValueName: ""; ValueData: "Vokabelheft";                Flags: uninsdeletekey deletevalue;
;Icon Zuordnen
Root: HKCR; Subkey: "vhf.File\DefaultIcon";        ValueType: string; ValueName: ""; ValueData: "{app}\icon_vhf.ico";         Flags: uninsdeletekey deletevalue;
;Aktion beim Klicken festlegen
Root: HKCR; Subkey: "vhf.File\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\Vocup.exe"" ""%1"""; Flags: uninsdeletekey deletevalue;
;Beschreibung der Aktion festlegen
Root: HKCR; Subkey: "vhf.File\shell\open";         ValueType: string; ValueName: ""; ValueData: "Mit Vocup öffnen";           Flags: uninsdeletekey deletevalue;

;Mit dieser Anweisung wird ein Registryschlüssel namens ".vdp" erzeugt
Root: HKCR; Subkey: ".vdp";                        ValueType: string; ValueName: ""; ValueData: "vdp.File";                   Flags: uninsdeletekey deletevalue;
;TypenSchlüssel erstellen
Root: HKCR; Subkey: "vdp.File";                    ValueType: string; ValueName: ""; ValueData: "Vocup Datensicherung";       Flags: uninsdeletekey deletevalue;
;Icon Zuordnen
Root: HKCR; Subkey: "vdp.File\DefaultIcon";        ValueType: string; ValueName: ""; ValueData: "{app}\icon_vdp.ico";         Flags: uninsdeletekey deletevalue;
;Aktion beim Klicken festlegen
Root: HKCR; Subkey: "vdp.File\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\Vocup.exe"" ""%1"""; Flags: uninsdeletekey deletevalue;
;Beschreibung der Aktion festlegen
Root: HKCR; Subkey: "vdp.File\shell\open";         ValueType: string; ValueName: ""; ValueData: "Mit Vocup öffnen";           Flags: uninsdeletekey deletevalue;

[Run]
Filename: {app}\Vocup.exe; Description: {cm:LaunchProgram,Vocup}; Flags: nowait postinstall skipifsilent;

[CustomMessages]
; TODO: TTF Font Microsoft Sans Serif von einem Windows PC kopieren
NetFxRequired_Caption0=Vocup benötigt den Font Sans Serif
NetFxRequired_Caption1=
NetFxRequired_Caption2=Das Setup konnte den Font Microsoft Sans Serif auf Ihrem System nicht finden. So werden Sie Vocup wahrscheinlich nicht starten können.
NetFxRequired_Caption3=
NetFxRequired_Caption4=Falls Sie Wine verwenden, können Sie sich die Datei C:\Windows\Fonts\micross.ttf von einem Windows System in Ihren Wine Prefix kopieren.

[Code]
function InitializeSetup(): boolean;
begin
    if not FileExists('C:\Windows\Fonts\micross.ttf') then begin
        MsgBox(
            ExpandConstant('{cm:NetFxRequired_Caption0}') + #13 +
            ExpandConstant('{cm:NetFxRequired_Caption1}') + #13 +
            ExpandConstant('{cm:NetFxRequired_Caption2}') + #13 +
            ExpandConstant('{cm:NetFxRequired_Caption3}') + #13 +
            ExpandConstant('{cm:NetFxRequired_Caption4}'),
            mbError,
            MB_OK);
    end;
    
    Result := true;
end;